grammar edu.tum.cs.cadmos.language.Cadmos with org.eclipse.xtext.common.Terminals

generate cadmos "http://www.tum.edu/cs/cadmos/language/Cadmos"

Model:
	('package' name=QualifiedName)?
	imports+=Import*
	elements+=ModelElement*;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

ModelElement:
	Component;

Component:
	'component' name=ID
	('(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')')?
	'{' (elements+=ComponentElement)* '}';

Parameter:
	name=ID '=' value=INT;

ComponentElement:
	NamedComponentElement | Channel;

NamedComponentElement:
	Port | Embedding;

enum PortDirection:
	Inbound='in' | Outbound='out';

Port:
	direction=PortDirection name=ID ('[' cardinality=Value ']')?;

Embedding:
	'embed' component=[Component]
	('(' (parameterAssignments+=ParameterAssignment (',' parameterAssignments+=ParameterAssignment)*)? ')')?
	name=ID ('[' cardinality=Value ']')?;

Channel:
	'channel' source=PortRef '->' destination=PortRef;

PortRef:
	(embedding=[Embedding] ('[' embeddingIndex=ID ']')? '.')? port=[Port] ('[' portIndex=ID ']')?;

Value:
	IntegerLiteral | ParameterRef;

IntegerLiteral:
	value=INT;

ParameterRef:
	parameter=[Parameter];

ParameterAssignment:
	left=[Parameter] '=' right=Value;

QualifiedName:
	ID ('.' ID)*;

QualifiedNameWithWildcard:
	QualifiedName '.*'?;
