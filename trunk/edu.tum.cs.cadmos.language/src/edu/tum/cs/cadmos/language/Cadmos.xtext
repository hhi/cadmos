grammar edu.tum.cs.cadmos.language.Cadmos with org.eclipse.xtext.xbase.Xbase

generate cadmos "http://www.tum.edu/cs/cadmos/language/Cadmos"
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

Model:
	('package' name=QualifiedName)?
	importSection=XImportSection?
	elements+=Element*;

Element:
	Component | Deployment;

Component:
	'component' name=ValidID
	'{' (features+=Feature)* '}';

Feature:
	Port | Embedding | Channel;

Port:
	(inbound?='in' | 'out') name=ValidID;

Embedding:
	'embed' component=[Component|QualifiedName] name=ValidID;

Channel:
	'channel' src=PortRef '->' snk=PortRef;

PortRef:
	(embedding=[Embedding] '.')? port=[Port];

Deployment:
	'deployment'
	'{' elements+=DeploymentElement* '}';

DeploymentElement:
	Constraint | Property;

Constraint:
	AllocationConstraint | AdditiveConstraint;

AdditiveConstraint:
	MemoryConsumption;

MemoryConsumption:
	'memoryConsumption' value=INT 'if' src=[Component] 'on' target=[Component] ;


AllocationConstraint:
	'allocate' source=[Component] 'to' '{' targets+=[Component] (',' targets+=[Component])* '}';

Property:
	AvailableMemory
;

AvailableMemory:
	'memory' value=INT 'on' component=[Component] 
;